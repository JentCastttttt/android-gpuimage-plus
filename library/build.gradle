apply plugin: 'com.android.library'

def usingCMakeCompile() {
    /// define  gradle.ext.usingCMakeCompile = false to disable CMakeCompile Mode.
    return gradle.ext != null && gradle.ext.has("usingCMakeCompile") && gradle.ext.usingCMakeCompile;
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    if (usingCMakeCompile()) {
        ndkVersion "23.1.7779620"
    }

    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        if (usingCMakeCompile()) {
            externalNativeBuild {
                cmake {
                    arguments "-DCGE_USE_VIDEO_MODULE=1",
                            "-DANDROID_STL=c++_static",
                            "-DANDROID_ARM_NEON=TRUE",
                            "-DANDROID_GRADLE=1",
                            "-DCGE_USE_VIDEO_MODULE=1"

                    cppFlags "-frtti -std=c++14"
                }
            }
        }
    }

    if (usingCMakeCompile()) {
        externalNativeBuild {
            cmake {
                path 'src/main/jni/CMakeLists.txt'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    ////////////////////////////////////////

    sourceSets.main {
         if (!usingCMakeCompile()) {
             jniLibs.srcDir 'src/main/libs' //set libs as .so's location instead of jni
             jni.srcDirs = [] //disable automatic ndk-build call with auto-generated Android.mk file
         }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:' + rootProject.ext.android.appcompatX
}
